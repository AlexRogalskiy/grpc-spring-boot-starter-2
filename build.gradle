plugins {
  id 'nebula.optional-base' version '3.0.3'
  id 'nebula.provided-base' version '3.0.3'
  id 'nebula.info-basic' version '3.0.3'
  id 'nebula.info-scm' version '3.0.3'
  id 'nebula.info-java' version '3.0.3'
  id 'nebula.nebula-release' version '4.0.1'
  id 'io.spring.dependency-management' version '0.5.6.RELEASE'
}

allprojects {
  repositories {
    maven { url "https://repo.spring.io/milestone" } // for using spring Brixton milestones
    jcenter()
  }

  apply plugin: 'idea'
  apply plugin: 'eclipse'

  apply plugin: 'nebula.info-scm'
  apply plugin: 'nebula.info-java'
  apply plugin: 'nebula.info-basic'
  apply plugin: 'io.spring.dependency-management'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'nebula.provided-base'
  apply plugin: 'nebula.optional-base'
  apply plugin: 'io.spring.dependency-management'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencyManagement {
    dependencies {
      dependencySet(group: 'org.springframework.boot', version: '1.3.3.RELEASE') {
        entry 'spring-boot-starter'
        entry 'spring-boot-configuration-processor'
        entry 'spring-boot-starter-actuator'
        entry 'spring-boot-starter-test'
      }
      dependency 'io.grpc:grpc-netty:0.13.1'
      dependency 'org.projectlombok:lombok:1.16.6'
      dependency 'org.codehaus.groovy:groovy-all:2.4.3'

      dependency 'org.spockframework:spock-core:1.0-groovy-2.4'
      dependency 'org.spockframework:spock-spring:1.0-groovy-2.4'
    }
  }

  dependencies {
    provided 'org.projectlombok:lombok'

    testCompile 'org.codehaus.groovy:groovy-all'
    testCompile 'org.spockframework:spock-core'
    testCompile 'org.spockframework:spock-spring'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.11'
}